//
// Created by intellij-pest on 2020-08-25
// grammar
// Author: enter
//

unit = { (!EOI ~ sexpr)* ~ EOI }

repl_unit = { !EOI ~ sexpr ~ EOI }

line_comment = _ {
	";" ~ (!NEWLINE ~ ANY)* ~ NEWLINE?
}

/*
block_comment = _ {
	"{" ~ (block_comment | (!"}" ~ ANY))* ~ "}"
}
*/

sexpr =
	{ list
    | quote
    | unquote
	| constant
	}

list =
	{ "(" ~ list_core ~ ")"
	| "[" ~ list_core ~ "]"
    | "{" ~ list_core ~ "}"
	}

list_core = {
    (sexpr ~ (!pair_right ~ sexpr)* ~ pair_right?)?
}

pair_right = {
    "." ~ WHITESPACE ~ sexpr
}

quote = { "\'" ~ sexpr }

unquote = { "," ~ sexpr }

quasiquote = { "`" ~ sexpr }

unquote_splicing = { ",@" ~ sexpr }

symbol = @{ (!(keyword | WHITESPACE | "\"" | "\'" | "`" | "," | ",@" | "(" | ")" | "[" | "]" | "{" | "}") ~ ANY)+ }

// -------------------------------------------------------

constant =
    { string_lit
	| float_lit
	| int_lit
	| uint_lit
    | bool_lit
    | nil_lit
    | symbol
}

int_lit = ${ signed_number ~ !("u" | "f") ~ "i"? }
uint_lit = ${ number ~ "u" }
float_lit = ${ float_number ~ "f"? }

float_number = $
    { (("+" | "-")? ~ number_hex ~ "." ~ ASCII_HEX_DIGIT+)
    | (("+" | "-")? ~ number_oct ~ "." ~ ASCII_OCT_DIGIT+)
    | (("+" | "-")? ~ number_bin ~ "." ~ ASCII_BIN_DIGIT+)
    | (("+" | "-")? ~ number_dec ~ "." ~ ASCII_DIGIT+)
}

signed_number = ${ ("+" | "-")? ~ number }

number = $
    { number_hex
    | number_oct
    | number_bin
    | number_dec
    }

number_dec = _ { ASCII_DIGIT+ }
number_oct = _ { "0o" ~ ASCII_OCT_DIGIT+ }
number_hex = _ { "0x" ~ ASCII_HEX_DIGIT+ }
number_bin = _ { "0b" ~ ASCII_BIN_DIGIT+ }

keyword = @{ bool_lit | nil_lit }

bool_lit = @{ kw_false | kw_true }

nil_lit = _ { "nil" }

kw_false = _ { "false" }
kw_true = _ { "true" }

string_lit = @ { "\"" ~ (escape | (!("\\" | "\"") ~ ANY)+)* ~ "\""}

// char_lit = @ { "\'" ~ (escape | ANY) ~ "\'" }

escape = _{ "\\\\"
		  | "\\\""
		  | "\\'"
		  | "\\n"
		  | "\\r"
		  | "\\t"
		  }

COMMENT = _
    { line_comment
    // | block_comment
    }

WHITESPACE = _
	{ " "
	| "\t"
	| WHITE_SPACE
	| NEWLINE
	}